PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gta: <https://schema.coypu.org/gta#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
 ?edge1 gta:hasAnnouncementDate ?date .
 ?edge2 gta:hasAnnouncementDate ?date .
} WHERE {
SELECT * WHERE {
  GRAPH <https://data.coypu.org/gta/> {
  {SELECT * {?a a gta:StateAct .} LIMIT 10000}
  ?a gta:hasAnnouncementDate ?date .
  ?a gta:hasIntervention ?i .
  ?i gta:hasAffectedSector ?s .
  GRAPH <https://data.coypu.org/products/cpc21/> {?s skos:broader ?ss . }
  
  ?i gta:hasImplementingJurisdiction ?ij .
  ?i gta:hasAffectedJurisdiction ?aj .
  
  BIND(<< ?ij gta:isImplementingInterventionOnSector ?ss >> AS ?edge1)
  BIND(<< ?aj gta:isAffectedByInterventionOnSector ?ss >> AS ?edge2)
}
}
  }
